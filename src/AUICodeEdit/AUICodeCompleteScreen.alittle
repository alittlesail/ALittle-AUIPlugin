
[Language(Lua)]
namespace AUIPlugin;

// 一条候选信息
protected struct AUICodeCompleteItemInfo
{
    // 整个候选容器
    ALittle.DisplayObject _item;
    // 候选按钮
    ALittle.TextRadioButton _item_button;
    // 候选文本
    ALittle.Text _item_title;
    // 候选图标
    ALittle.Image _tag_image;

    // 搜索命中的下标，用于候选列表排序
    List<int> pos;
    // 候选内容转为大写
    string upper;
    // 候选详细信息
    lua.ABnfQueryCompleteInfo complete;
}

// 候选滚动屏
protected class AUICodeCompleteScreen
{
    // 滚动屏幕
    private ALittle.ScrollScreen _screen;
    // 当前所有候选信息列表
    private List<AUICodeCompleteItemInfo> _item_list;
    // 构造对象池
    private List<AUICodeCompleteItemInfo> _item_pool = new List<AUICodeCompleteItemInfo>();
    private int _item_pool_count = 0;
    // 对应候选内容的提示
    private ALittle.DisplayObject _tip_dialog;
    private ALittle.TextArea _tip_text;

    // 所有的候选列表信息
    private lua.ABnfQueryComplete _complete;
    
    // 当前行和列
    private int _line_end;
    private int _char_end;
    private AUICodeEdit _edit; // 编辑控件
    
    // 每个候选项的高度，这个由编辑器编辑的控件高度觉得
    private double _item_height = 0;

    public ctor(AUICodeEdit edit)
    {
        this._edit = edit;
    }

    // 显示候选列表
    public async fun ShowComplete()
    {
        // 如果当前没有候选列表，那么要进行初始化
        if (this._complete == null)
        {
            // 初始化候选列表
            if (!this.ReInit())
            {
                // 失败了，直接隐藏然后返回
                this.Hide();
                return;
            }
        }

        // 保存当前的光标行和列
        this._line_end = this._edit.cursor.line;
        this._char_end = this._edit.cursor.char;
        // 抓取识别的文本内容，如果没有或者为空串，直接返回
        var text = this._edit.GetTargetText(this._complete.line_start, this._complete.char_start - 1, this._edit.cursor.line, this._edit.cursor.char);
        if (text == null || text == "")
        {
            this.Hide();
            return;
        }

        // 根据text的内容，对候选列表信息进行过滤
        if (!this.Fliter(text))
        {
            // 过滤失败那么就重新抓取信息
            this.Hide();
            if (!this.ReInit()) return;

            // 保存当前的光标行和列
            this._line_end = this._edit.cursor.line;
            this._char_end = this._edit.cursor.char;
            // 抓取识别的文本内容，如果没有或者为空串，直接返回
            text = this._edit.GetTargetText(this._complete.line_start, this._complete.char_start - 1, this._edit.cursor.line, this._edit.cursor.char);
            if (text == null || text == "") return;
            // 根据text的内容，对候选列表信息进行过滤
            if (!this.Fliter(text)) this.Hide();
        }
    }

    // 是否处于显示状态
    public fun IsShow() : bool { return this._complete != null; }

    // 向上选择
    public fun SelectUp()
    {
        // 获取当前选择的下标
        var target_index = this.GetSelectIndex();
        if (target_index == null) target_index = 1;
        else target_index -= 1;
        // 如果已经是第一个了，那么就直接返回
        if (target_index < 1) return;
        // 获取对应项
        var item = this._screen.childs[target_index];
        // 获取项信息
        var info = cast<AUICodeCompleteItemInfo>(item._user_data);
        // 把单选按钮设置为选中
        info._item_button.selected = true;
        // 如果有描述信息，那么就显示tip，否则就关闭
        if (info.complete.descriptor != null)
            this.ShowTip(info.complete.descriptor);
        else
            this.HideTip();
        // 调整滚动条位置
        var delta = this._screen.container.height - this._screen.height;
        if (delta > 0)
        {
            var offset = (target_index - 1) * this._item_height + this._screen.container_y;
            if (offset < 0)
            {
                this._screen.right_scrollbar.offset_rate = ((target_index - 1) * this._item_height) / delta; 
                this._screen.AdjustScrollBar();
            }
        }
    }

    public fun SelectDown()
    {
        // 获取当前选择的下标
        var target_index = this.GetSelectIndex();
        if (target_index == null) target_index = 1;
        else target_index += 1;
        // 如果已经是最后一个了，那么就直接返回
        if (target_index > this._screen.child_count) return;
        // 获取对应项
        var item = this._screen.childs[target_index];
        // 获取项信息
        var info = cast<AUICodeCompleteItemInfo>(item._user_data);
        // 把单选按钮设置为选中
        info._item_button.selected = true;
        // 如果有描述信息，那么就显示tip，否则就关闭
        if (info.complete.descriptor != null)
            this.ShowTip(info.complete.descriptor);
        else
            this.HideTip();
        // 调整滚动条
        var delta = this._screen.container.height - this._screen.height;
        if (delta > 0)
        {
            var offset = target_index * this._item_height + this._screen.container_y;
            if (offset > this._screen.height)
            {
                this._screen.right_scrollbar.offset_rate = (target_index * this._item_height - this._screen.height) / delta;   
                this._screen.AdjustScrollBar();   
            }
        }
    }

    public fun DoSelect() : bool
    {
        // 获取当前选择的下标
        var target_index = this.GetSelectIndex();
        if (target_index == null) return false;
        // 获取选中项
        var item = this._screen.childs[target_index];
        // 获取项信息
        var complete = cast<AUICodeCompleteItemInfo>(item._user_data).complete;
        // 如果有insert字段，那么就选insert字段，如果没有就选display字段
        var text = complete.insert;
        if (text == null) text = complete.display;
        // 如果文本没有，那么就直接返回
        if (text == null) return false;
        // 设置区域光标
        this._edit.select_cursor.StartLineChar(this._complete.line_start, this._complete.char_start - 1);
        this._edit.select_cursor.UpdateLineChar(this._edit.cursor.line, this._edit.cursor.char);
        // 插入
        var result = this._edit.InsertText(text, true);
        // 隐藏
        this.Hide();
        // 返回结果
        return result;
    }

    // 获取当前选中的下标
    private fun GetSelectIndex() : int
    {
        var target:int = null;
        for (var index, child in this._screen.childs)
        {
            if (cast<AUICodeCompleteItemInfo>(child._user_data)._item_button.selected)
            {
                target = index;
                break;
            }   
        }
        return target;
    }

    // 初始化候选列表
    private await fun ReInit() : bool
    {
        // 如果没有语言插件，那么就直接返回
        if (this._edit.language == null) return false;
        // 查询候选信息
        this._complete = this._edit.language.QueryComplete(this._edit.cursor.line, this._edit.cursor.char - 1);
        // 如果查询不到，那么就直接返回
        if (this._complete == null) return false;
        // 计算坐标
        var x, y = this._edit.CalcPosition(this._complete.line_start, this._complete.char_start, true);
        y += CODE_LINE_HEIGHT;
        // 清空滚动屏
        if (this._screen == null)
        {
            this._screen = g_Control.CreateControl{ALittle.ScrollScreen}("code_scroll_screen");
            this._screen.width = 200;
        }
        this._screen.RemoveAllChild();
        // 设置滚动屏位置
        this._screen.x = x;
        this._screen.y = y;
        // 把当前所有的候选项全部纳入到池
        if (this._item_list != null)
        {
            for (var index, info in this._item_list)
            {
                this._item_pool_count += 1;
                this._item_pool[this._item_pool_count] = info;
            }
        }
        // 设置最大宽度
        var max_width = 200.0;

        // 构建最新的候选组
        var item_group = new ALittle.TextRadioButtonGroup();
        // 构建最新的候选列表
        this._item_list = new List<AUICodeCompleteItemInfo>();
        // 遍历候选列表
        for (var index, info in this._complete.complete_list)
        {
            // 构建候选项
            var item_info:AUICodeCompleteItemInfo;
            if (this._item_pool_count > 0)
            {
                item_info = this._item_pool[this._item_pool_count];
                this._item_pool[this._item_pool_count] = null;
                this._item_pool_count -= 1;
            }
            else
            {
                item_info = new AUICodeCompleteItemInfo();   
                item_info._item = g_Control.CreateControl{ALittle.DisplayObject}("code_complete_item", item_info);
            }
            // 设置候选项的组
            item_info._item_button.group = item_group;
            item_info._item_button.selected = false;
            // 设置候选项标题
            item_info._item_title.text = info.display;
            var text:string = info.insert;
            if (text == null) text = info.display;
            if (text != null) item_info.upper = ALittle.String_Upper(text);
            else item_info.upper = null;
            // 设置图标
            item_info._tag_image.texture_name = this._edit.language.QueryCompleteIcon(info.tag);
            // 关联项信息
            item_info._item._user_data = item_info;
            // 保存信息
            item_info.complete = info;
            // 添加到列表
            this._item_list[index] = item_info;
            // 计算当前宽度
            var title_wdith = item_info._item_title.width + item_info._item_title.x + 5;
            if (max_width < title_wdith) max_width = title_wdith;
            // 保存每项高度
            this._item_height = item_info._item.height;
        }
        // 设置滚动屏宽度
        this._screen.width = max_width + this._screen.right_scrollbar.width;
        // 添加到容器
        this._edit.help_container.AddChild(this._screen);
        return true;
    }

    // 排序
    private static ItemInfoSort(AUICodeCompleteItemInfo a, AUICodeCompleteItemInfo b) : bool
    {
        for (var index, pos in a.pos)
        {
            if (b.pos[index] == null) return true;
            if (pos != b.pos[index]) return pos < b.pos[index];
        }
        return false;
    }

    // 根据文本过滤
    private fun Fliter(string text) : bool
    {
        // 将内容改为大写
        var upper_text = ALittle.String_Upper(text);
        // 找出第一个分隔符
        var first_split = ALittle.String_Find(upper_text, "_");
        if (first_split == null) first_split = 1;
        else first_split += 1;
        // 切割大写，并且要跳过第一个分隔符
        var upper_list = ALittle.String_Split(upper_text, "_", first_split);
        // 将第一个分隔符前面的字符添加到列表
        if (first_split > 1) ALittle.List_Insert(upper_list, 1, ALittle.String_Sub(upper_text, 1, first_split - 1));
        // 如果数组长度大于0，并且最后一个是空串，那么就剔除掉
        var upper_list_count = ALittle.List_Len(upper_list);
        if (upper_list_count > 1 && upper_list[upper_list_count] == "") upper_list[upper_list_count] = null;

        // 列表
        var sort_list = new List<AUICodeCompleteItemInfo>();
        var count = 0;
        // 移除所有项
        this._screen.RemoveAllChild();
        // 把符合条件的全部添加到sort_list
        for (var index, info in this._item_list)
        {
            // 初始化数据
            info.pos = null;
            var pos = 1;
            // 遍历匹配列表
            for (var _, upper in upper_list)
            {
                // 查找upper进行匹配
                pos = ALittle.String_Find(info.upper, upper, pos);
                if (pos != null)
                {
                    // 如果是第一次设置，那么就加入列表
                    if (info.pos == null)
                    {
                        info.pos = new List<int>();
                        ++count;
                        sort_list[count] = info;
                    }

                    ALittle.List_Push(info.pos, pos);
                }
            }
        }

        // 排序
        ALittle.List_Sort(sort_list, AUICodeCompleteScreen.ItemInfoSort);

        // 添加前面50个
        for (var index, info in sort_list)
        {
            this._screen.AddChild(info._item); 
            if (index >= 50) break;
        }
        if (this._screen.child_count == 0) return false;
        
        // 计算高度
        var height = this._screen.child_count * this._item_height;
        if (height < 200) this._screen.height = height;
        else this._screen.height = 200;

        // 计算坐标正在编辑的目标的位置
        var edit_x, edit_y = this._edit.CalcPosition(this._edit.cursor.line, this._edit.cursor.char, true);

        // 根据高度，调整候选框位置
        var x, y = this._screen.LocalToGlobal(this._edit);
        if (y + this._screen.height > this._edit.height || this._screen.y + this._screen.height < edit_y)
            this._screen.y = edit_y - this._screen.height;

        return true;
    }

    public fun Hide()
    {
        // 隐藏tip提示
        this.HideTip();
        // 把候选内容置为null
        this._complete = null;
        // 清空滚动屏所有项
        if (this._screen != null) this._screen.RemoveAllChild();
        // 从空间树上移除
        this._edit.help_container.RemoveChild(this._screen);
    }

    // 如果当前的光标在候选目标文本之外，那么隐藏
    public fun TryHide()
    {
        // 如果没有候选信息，那么直接返回
        if (this._complete == null) return;

        // 检查行
        if (this._edit.cursor.line < this._complete.line_start || this._edit.cursor.line > this._line_end)
        {
            this.Hide();
            return;
        }
        // 检查开始行
        if (this._edit.cursor.line == this._complete.line_start && this._edit.cursor.char < this._complete.char_start)
        {
            this.Hide();
            return;
        }
        // 检查结束行
        if (this._edit.cursor.line == this._line_end && this._edit.cursor.char > this._char_end)
        {
            this.Hide();
            return;
        }
    }

	// 显示TIP
	private fun ShowTip(string content)
	{
		// 创建模态提示对话框，并添加到模态层
		if (this._tip_dialog == null)
		{
			this._tip_dialog = g_Control.CreateControl{ALittle.DisplayObject}("code_area_tip", this);
            this._tip_dialog.width = 200;
		}

        // 将对话框添加到容器
        this._edit.help_container.AddChild(this._tip_dialog);
        // 显示
		this._tip_dialog.visible = true;
        // 设置文本内容
		this._tip_text.text = content;
        // 设置高度
		this._tip_dialog.height = this._tip_text.real_height + 16;
        // 调整位置
		this._tip_dialog.x = this._screen.x + this._screen.width;
		this._tip_dialog.y = this._screen.y;
        if (this._tip_dialog.x + this._tip_dialog.width > A_UISystem.view_width)
        {
            this._tip_dialog.x = this._screen.x;
            this._tip_dialog.y = this._screen.y + this._screen.height;
        }
	}

	// 隐藏TIP
	private fun HideTip()
	{
		if (this._tip_dialog == null) return;
		this._tip_dialog.visible = false;
        this._edit.help_container.RemoveChild(this._tip_dialog);
	}
}